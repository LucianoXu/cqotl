/-
Copyright (c) 2025 IvÃ¡n Renison. All rights reserved.
Authors: IvÃ¡n Renison, Jam Khan
-/

import Mathlib.Analysis.InnerProductSpace.Positive
import Mathlib.Analysis.InnerProductSpace.Projection
import Mathlib.Analysis.InnerProductSpace.Spectrum
import Mathlib.LinearAlgebra.Trace

import LeanVeri.Sum

open scoped ComplexOrder

/-!
# Some basic propositions about `LinearMap`

This file contains some basic propositions about `LinearMap` that are not already in Mathlib.
Some of this may be later added to Mathlib.

We use `inner ğ•œ (T x) x` in several places, this is what in quantum is some times denotes as `âŸ¨x|T|xâŸ©`.
-/

variable {ğ•œ E F : Type*} [RCLike ğ•œ]

variable? [HilbertSpace ğ•œ E] => [NormedAddCommGroup E] [InnerProductSpace ğ•œ E] [CompleteSpace E]
variable [FiniteDimensional ğ•œ E]

variable? [HilbertSpace ğ•œ F] => [NormedAddCommGroup F] [InnerProductSpace ğ•œ F] [CompleteSpace F]
variable [FiniteDimensional ğ•œ F]

namespace LinearMap

/-- Positive semidefinite operators. -/
def isPositiveSemiDefinite (T : E â†’â‚—[ğ•œ] E) : Prop :=
  IsSelfAdjoint T âˆ§ âˆ€ x, 0 â‰¤ RCLike.re (inner ğ•œ (T x) x)

/-- Positive definite operators. -/
def isPositiveDefinite (T : E â†’â‚—[ğ•œ] E) : Prop :=
  IsSelfAdjoint T âˆ§ âˆ€ x, 0 < RCLike.re (inner ğ•œ (T x) x)

/-- Partial density operators. -/
def isPartialDensityOperator (T : E â†’â‚—[ğ•œ] E) : Prop :=
  T.isPositiveSemiDefinite âˆ§ trace ğ•œ E T â‰¤ 1

/-- Density operators. -/
def isDensityOperator (T : E â†’â‚—[ğ•œ] E) : Prop :=
  T.isPositiveSemiDefinite âˆ§ trace ğ•œ E T = 1

/-- Quantum predicate. -/
def isEffect (T : E â†’â‚—[ğ•œ] E) : Prop :=
  T.isPositiveSemiDefinite âˆ§ (id - T).isPositiveSemiDefinite

/-- Isometry operators. -/
def isIsometry (T : E â†’â‚—[ğ•œ] F) : Prop :=
  T.adjoint âˆ˜â‚— T = id

/-- Unitary operators. The same as isometry, but for `E â†’â‚—[ğ•œ] E`.
In Mathlib we have `IsUnit T`, but it is a different thing. -/
def isUnitary (T : E â†’â‚—[ğ•œ] E) : Prop :=
  T.isIsometry

/-- Projection operator -/
def isProjection (T : E â†’â‚—[ğ•œ] E) : Prop :=
  T.isPositiveSemiDefinite âˆ§ T âˆ˜â‚— T = T

/-- LÃ¶wner order between operators. -/
def LoewnerOrder (T N : E â†’â‚—[ğ•œ] E) : Prop :=
  (N - T).isPositiveSemiDefinite

/-- Pure state operators. -/
def isPureState (T : E â†’â‚—[ğ•œ] E) : Prop :=
  T.isDensityOperator âˆ§ T.rank = 1

omit [CompleteSpace E]

lemma isPositiveSemiDefinite.zero : (0 : E â†’â‚—[ğ•œ] E).isPositiveSemiDefinite := by
  apply And.intro
  Â· exact IsSelfAdjoint.zero (E â†’â‚—[ğ•œ] E)
  Â· intro x
    simp [zero_apply]

lemma isPositiveSemiDefinite.one : (1 : E â†’â‚—[ğ•œ] E).isPositiveSemiDefinite := by
  apply And.intro
  Â· exact (isSymmetric_iff_isSelfAdjoint 1).mp fun x â†¦ congrFun rfl
  Â· intro x
    exact inner_self_nonneg

lemma isProjection.zero : (0 : E â†’â‚—[ğ•œ] E).isProjection := And.intro isPositiveSemiDefinite.zero rfl

lemma isProjection.one : (1 : E â†’â‚—[ğ•œ] E).isProjection := And.intro isPositiveSemiDefinite.one rfl

lemma isProjection.apply_range {T : E â†’â‚—[ğ•œ] E} (hT : T.isProjection) {x : E} (hx : x âˆˆ range T) :
    T x = x := by
  obtain âŸ¨y, hyâŸ© := hx
  rw [â† hy, â† comp_apply, hT.right]

lemma isPositiveSemiDefinite.IsSymmetric (T : E â†’â‚—[ğ•œ] E) (hT : T.isPositiveSemiDefinite) : T.IsSymmetric :=
  (isSymmetric_iff_isSelfAdjoint T).mpr hT.left

lemma isPositiveSemiDefinite_add_of_isPositiveSemiDefinite {T S : E â†’â‚—[ğ•œ] E} (hT : T.isPositiveSemiDefinite)
    (hS : S.isPositiveSemiDefinite) : (T + S).isPositiveSemiDefinite := by
  apply And.intro
  Â· unfold IsSelfAdjoint
    rw [star_add, hT.left, hS.left]
  Â· intro x
    rw [add_apply, inner_add_left, AddMonoidHom.map_add]
    exact Left.add_nonneg (hT.right x) (hS.right x)

lemma isPositiveSemiDefinite_real_smul_of_isPositiveSemiDefinite {T : E â†’â‚—[ğ•œ] E} (hT : T.isPositiveSemiDefinite) {c : â„}
    (hc : 0 â‰¤ c) : ((c : ğ•œ) â€¢ T).isPositiveSemiDefinite := by
  apply And.intro
  Â· rw [â† isSymmetric_iff_isSelfAdjoint]
    apply IsSymmetric.smul (RCLike.conj_ofReal c) hT.IsSymmetric
  Â· intro x
    rw [smul_apply]
    rw [inner_smul_left]
    rw [RCLike.conj_ofReal]
    rw [RCLike.re_ofReal_mul]
    exact Left.mul_nonneg hc (hT.right x)

lemma isPositiveSemiDefinite_real_smul_of_isPositiveSemiDefinite' {c : ğ•œ} (hc : 0 â‰¤ c) {T : E â†’â‚—[ğ•œ] E}
    (hT : T.isPositiveSemiDefinite)  : (c â€¢ T).isPositiveSemiDefinite := by
  let c' : â„ := RCLike.re c
  have hstarc : (starRingEnd ğ•œ) c = c := by
    rw [RCLike.conj_eq_iff_im]
    simp [â† (RCLike.le_iff_re_im.mp hc).right]
  have hcc' : c = c' := by
    rw [RCLike.ext_iff]
    apply And.intro
    Â· simp [c']
    Â· simp only [RCLike.ofReal_im, c']
      exact RCLike.conj_eq_iff_im.mp hstarc
  have hc' : 0 â‰¤ c' := by
    rw [â† @RCLike.zero_re ğ•œ]
    exact (RCLike.le_iff_re_im.mp hc).left
  rw [hcc']
  exact isPositiveSemiDefinite_real_smul_of_isPositiveSemiDefinite hT hc'

lemma isPositiveSemiDefinite.sub_of_LoewnerOrder {T S : E â†’â‚—[ğ•œ] E} (h : T.LoewnerOrder S) :
    (S - T).isPositiveSemiDefinite := by
  apply And.intro
  Â· rw [â† isSymmetric_iff_isSelfAdjoint]
    exact h.IsSymmetric
  Â· exact h.right

lemma isPositiveSemiDefinite.nonneg_real_smul {T : E â†’â‚—[ğ•œ] E} (hT : T.isPositiveSemiDefinite)
    {c : â„} (hc : 0 â‰¤ c) : ((c : ğ•œ) â€¢ T).isPositiveSemiDefinite := by
  apply And.intro
  Â· rw [â† isSymmetric_iff_isSelfAdjoint]
    exact IsSymmetric.smul (RCLike.conj_ofReal c) hT.IsSymmetric
  Â· intro x
    rw [smul_apply, inner_smul_left, RCLike.conj_ofReal, RCLike.re_ofReal_mul]
    exact Left.mul_nonneg hc (hT.right x)

lemma isPositiveSemiDefinite.nonneg_eigenvalues {T : E â†’â‚—[ğ•œ] E} (hT : T.isPositiveSemiDefinite)
    (i : Fin (Module.finrank ğ•œ E)) : 0 â‰¤ hT.IsSymmetric.eigenvalues rfl i := by
  have h := hT.right (hT.IsSymmetric.eigenvectorBasis rfl i)
  rw [hT.IsSymmetric.apply_eigenvectorBasis, inner_smul_real_left, RCLike.smul_re,
    inner_self_eq_norm_sq, OrthonormalBasis.norm_eq_one] at h
  simp only [one_pow, mul_one] at h
  exact h

omit [FiniteDimensional ğ•œ E] in
lemma eq_zero_iff_forall_re_inner_eq_zero (T : E â†’â‚—[ğ•œ]E) :
    T = 0 â†” âˆ€x : E, âˆ€y : E, RCLike.re (inner ğ•œ (T x) y) = 0 := by
  apply Iff.intro
  Â· intro h
    simp [h]
  Â· intro h
    apply LinearMap.ext
    intro x
    have hTx := h x (T x)
    rw [inner_self_eq_norm_mul_norm, mul_self_eq_zero] at hTx
    exact norm_eq_zero.mp hTx

lemma IsSelfAdjoint.re_inner_app_self_eq_zero_iff_app_eq_zero {T : E â†’â‚—[ğ•œ]E} (hT : IsSelfAdjoint T) :
    T = 0 â†” âˆ€x : E, RCLike.re (inner ğ•œ (T x) x) = 0 := by
  apply Iff.intro
  Â· intro h
    simp [h]
  Â· intro h
    have aux : âˆ€x : E, âˆ€y : E, RCLike.re (inner ğ•œ (T x) y) = RCLike.re ((inner ğ•œ (T (x + y)) (x + y) - inner ğ•œ (T (x - y)) (x - y))) / 4 := by
      intro x y
      simp only [inner_add_left, inner_add_right, inner_sub_left, inner_sub_right, map_sub, map_add, inner_re_symm (T y) x]
      rw [(isSymmetric_iff_isSelfAdjoint T).mpr hT]
      ring_nf
    have h' : âˆ€x : E, âˆ€y : E, RCLike.re (inner ğ•œ (T x) y) = 0 := by
      intro x y
      rw [aux, map_sub, h (x + y), h (x - y)]
      ring
    apply (eq_zero_iff_forall_re_inner_eq_zero T).mpr h'

lemma isPositiveSemiDefinite.eq_iff_forall_re_inner_app_self_eq {T N : E â†’â‚—[ğ•œ]E} (hT : IsSelfAdjoint T)
    (hN : IsSelfAdjoint N) :
    T = N â†” âˆ€x : E, RCLike.re (inner ğ•œ (T x) x) = RCLike.re (inner ğ•œ (N x) x) := by
  have hT' : T.IsSymmetric := (isSymmetric_iff_isSelfAdjoint T).mpr hT
  have hN' : N.IsSymmetric := (isSymmetric_iff_isSelfAdjoint N).mpr hN
  have hTN : IsSelfAdjoint (T - N) := (isSymmetric_iff_isSelfAdjoint (T - N)).mp (hT'.sub hN')
  apply Iff.intro
  Â· intro h x
    rw [h]
  Â· intro h
    have hTN' : âˆ€x : E, RCLike.re (inner ğ•œ ((T - N) x) x) = 0 := by
      intro x
      rw [sub_apply, inner_sub_left, map_sub, sub_eq_zero]
      exact h x
    rw [â† sub_eq_zero]
    exact (IsSelfAdjoint.re_inner_app_self_eq_zero_iff_app_eq_zero hTN).mpr hTN'

/--
Characterization of when `RCLike.re (inner ğ•œ (T x) x)` is zero.
The proof works be decomposing `x` in the eigenbasis of `T`.
-/
lemma isPositiveSemiDefinite.re_inner_app_eq_zero_iff_app_eq_zero {T : E â†’â‚—[ğ•œ]E} (hT : T.isPositiveSemiDefinite) (x : E) :
    RCLike.re (inner ğ•œ (T x) x) = 0 â†” T x = 0 := by
  have hTsymm : T.IsSymmetric := hT.IsSymmetric
  let n : â„• := Module.finrank ğ•œ E
  have hn : Module.finrank ğ•œ E = n := rfl
  let base : OrthonormalBasis (Fin n) ğ•œ E := hTsymm.eigenvectorBasis hn
  let x_repr : EuclideanSpace ğ•œ (Fin n) := base.repr x
  apply Iff.intro
  Â· intro hx
    let diag : Finset (Fin n Ã— Fin n) := Finset.univ.filter fun i â†¦ i.1 = i.2
    have : RCLike.re (inner ğ•œ (T x) x) = âˆ‘ i, â€–base.repr x iâ€– ^ 2 * hTsymm.eigenvalues hn i := calc
      RCLike.re (inner ğ•œ (T x) x)
        = RCLike.re (inner ğ•œ (T (âˆ‘ i, base.repr x i â€¢ base i)) (âˆ‘ i, base.repr x i â€¢ base i)) := by rw [OrthonormalBasis.sum_repr base x]
      _ = RCLike.re (inner ğ•œ (âˆ‘ i, T (base.repr x i â€¢ base i)) (âˆ‘ i, base.repr x i â€¢ base i)) := by rw [map_sum T _ Finset.univ]
      _ = âˆ‘ i, âˆ‘ j, RCLike.re (inner ğ•œ (base.repr x j â€¢ T (base j)) (base.repr x i â€¢ base i)) := by simp [inner_sum, sum_inner]
      _ = âˆ‘ i, âˆ‘ j, RCLike.re (starRingEnd ğ•œ (base.repr x j) *
          (starRingEnd ğ•œ â†‘(hTsymm.eigenvalues hn j) * (base.repr x i * inner ğ•œ (base j) (base i)))
        ) := by
          apply Fintype.sum_congr _ _
          intro i
          apply Fintype.sum_congr _ _
          intro j
          rw [hTsymm.apply_eigenvectorBasis, InnerProductSpace.smul_left, InnerProductSpace.smul_left, inner_smul_right_eq_smul]
          rfl
      _ = âˆ‘ ij : Fin n Ã— Fin n, RCLike.re (starRingEnd ğ•œ (base.repr x ij.2) *
          (starRingEnd ğ•œ â†‘(hTsymm.eigenvalues hn ij.2) * (base.repr x ij.1 * inner ğ•œ (base ij.2) (base ij.1)))
        ) := by rw [â† Fintype.sum_prod_type']
      _ = âˆ‘ ij âˆˆ diag, RCLike.re (starRingEnd ğ•œ (base.repr x ij.2) *
          (starRingEnd ğ•œ â†‘(hTsymm.eigenvalues hn ij.2) * (base.repr x ij.1 * inner ğ•œ (base ij.2) (base ij.1)))
        ) + âˆ‘ ij âˆˆ diagá¶œ, RCLike.re (starRingEnd ğ•œ (base.repr x ij.2) *
          (starRingEnd ğ•œ â†‘(hTsymm.eigenvalues hn ij.2) * (base.repr x ij.1 * inner ğ•œ (base ij.2) (base ij.1)))
        ) := by rw [Finset.sum_add_sum_compl diag]
      _ = âˆ‘ ij âˆˆ diag, RCLike.re (starRingEnd ğ•œ (base.repr x ij.2) *
          (starRingEnd ğ•œ â†‘(hTsymm.eigenvalues hn ij.2) * base.repr x ij.1)
        ) + âˆ‘ ij âˆˆ diagá¶œ, RCLike.re (starRingEnd ğ•œ (base.repr x ij.2) *
          (starRingEnd ğ•œ â†‘(hTsymm.eigenvalues hn ij.2) * (base.repr x ij.1 * inner ğ•œ (base ij.2) (base ij.1)))
        ) := by
          have hdiag : âˆ€ij âˆˆ diag, inner ğ•œ (base ij.2) (base ij.1) = 1 := by
            intro ij hij
            unfold diag at hij
            simp only [Finset.mem_filter, Finset.mem_univ, true_and] at hij
            rw [hij]
            have hnorm : â€–base ij.2â€– = 1 := base.norm_eq_one ij.2
            exact (inner_eq_one_iff_of_norm_one hnorm hnorm).mpr rfl
          simp +contextual [hdiag]
      _ = âˆ‘ ij âˆˆ diag, RCLike.re (starRingEnd ğ•œ (base.repr x ij.2) *
          (starRingEnd ğ•œ â†‘(hTsymm.eigenvalues hn ij.2) * base.repr x ij.1)
        ) := by
          have hdiagc : âˆ€ij âˆˆ diagá¶œ, inner ğ•œ (base ij.2) (base ij.1) = 0 := by
            intro ij hij
            unfold diag at hij
            simp at hij
            exact base.inner_eq_zero fun heq â†¦ hij (heq.symm)
          simp +contextual [hdiagc]
      _ = âˆ‘ i, RCLike.re (starRingEnd ğ•œ (base.repr x i) *
          (starRingEnd ğ•œ â†‘(hTsymm.eigenvalues hn i) * base.repr x i)
        ) := by
          let f : Fin n â†’ Fin n â†’ â„ := fun i j â†¦
            RCLike.re (starRingEnd ğ•œ (base.repr x j) * (starRingEnd ğ•œ â†‘(hTsymm.eigenvalues hn j) * base.repr x i))
          unfold diag
          apply sum_diag_eq n f
      _ = âˆ‘ i, RCLike.re (starRingEnd ğ•œ (base.repr x i) * base.repr x i *
          â†‘(hTsymm.eigenvalues hn i)
        ) := by
          apply Fintype.sum_congr _ _
          intro i
          rw [RCLike.conj_ofReal]
          ring_nf
      _ = âˆ‘ i, RCLike.re ((â€–base.repr x iâ€– : ğ•œ)^2 * â†‘(hTsymm.eigenvalues hn i)) := by
          simp +contextual [RCLike.conj_mul]
      _ = âˆ‘ i, (â€–base.repr x iâ€–^2 * hTsymm.eigenvalues hn i) := by
          apply Fintype.sum_congr _ _
          intro i
          rw [â† RCLike.ofReal_pow, â† RCLike.ofReal_mul, RCLike.ofReal_re]
    rw [this] at hx
    have : âˆ€i, 0 â‰¤ â€–base.repr x iâ€– ^ 2 * hTsymm.eigenvalues hn i := by
      intro i
      rw [mul_nonneg_iff]
      apply Or.inl
      apply And.intro
      Â· exact sq_nonneg â€–base.repr x iâ€–
      Â· exact hT.nonneg_eigenvalues i
    rw [Fintype.sum_eq_zero_iff_of_nonneg this, funext_iff] at hx
    simp only [Pi.zero_apply, mul_eq_zero, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, pow_eq_zero_iff, norm_eq_zero] at hx
    rw [â† base.sum_repr x, map_sum T _ Finset.univ]
    simp only [map_smul]
    simp_rw [base, hTsymm.apply_eigenvectorBasis]
    simp +contextual only [smul_smul]
    apply Fintype.sum_eq_zero
    intro i
    apply smul_eq_zero_of_left
    have hxi : base.repr x i = 0 âˆ¨ hTsymm.eigenvalues hn i = 0 := hx i
    apply hxi.elim
    Â· intro hxi0
      exact mul_eq_zero_of_left hxi0 â†‘(hTsymm.eigenvalues hn i)
    Â· intro hxi0
      apply mul_eq_zero_of_right
      exact RCLike.ofReal_eq_zero.mpr hxi0
  Â· intro hx
    rw [hx]
    simp

theorem isPositiveSemiDefinite.inner_app_eq_zero_iff_app_eq_zero {T : E â†’â‚—[ğ•œ]E} (hT : T.isPositiveSemiDefinite) (x : E) :
    inner ğ•œ (T x) x = 0 â†” T x = 0 := by
  apply Iff.intro
  Â· intro hx
    have hx' : RCLike.re (inner ğ•œ (T x) x) = 0 := by
      rw [hx]
      exact RCLike.zero_re
    exact (re_inner_app_eq_zero_iff_app_eq_zero hT x).mp hx'
  Â· intro hx
    rw [hx]
    simp

lemma LoewnerOrder_iff_of_isPositiveSemiDefinite {T N : E â†’â‚—[ğ•œ] E} (hT : T.isPositiveSemiDefinite)
    (hN : N.isPositiveSemiDefinite) :
    T.LoewnerOrder N â†” âˆ€x : E, 0 â‰¤ RCLike.re (inner ğ•œ ((N - T) x) x) := by
  apply Iff.intro
  Â· intro h
    exact (isPositiveSemiDefinite.sub_of_LoewnerOrder h).right
  Â· intro h
    exact And.intro (IsSelfAdjoint.sub hN.left hT.left) h

lemma LoewnerOrder_iff_of_isPositiveSemiDefinite' {T N : E â†’â‚—[ğ•œ] E} (hT : T.isPositiveSemiDefinite)
    (hN : N.isPositiveSemiDefinite) :
    T.LoewnerOrder N â†” âˆ€x : E, RCLike.re (inner ğ•œ (T x) x) â‰¤ RCLike.re (inner ğ•œ (N x) x) := by
  rw [LoewnerOrder_iff_of_isPositiveSemiDefinite hT hN]
  apply forall_congr'
  intro x
  calc
    0 â‰¤ RCLike.re (inner ğ•œ ((N - T) x) x)
    â†” 0 â‰¤ RCLike.re (inner ğ•œ (N x - T x) x) := by rfl
  _ â†” 0 â‰¤ RCLike.re (inner ğ•œ (N x) x - inner ğ•œ (T x) x) := by rw [inner_sub_left]
  _ â†” 0 â‰¤ RCLike.re (inner ğ•œ (N x) x) - RCLike.re (inner ğ•œ (T x) x) := by rw [map_sub]
  _ â†” RCLike.re (inner ğ•œ (T x) x) â‰¤ RCLike.re (inner ğ•œ (N x) x) := by apply sub_nonneg

end LinearMap
