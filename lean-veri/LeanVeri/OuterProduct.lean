/-
Copyright (c) 2025 IvÃ¡n Renison, Jam Khan. All rights reserved.
Authors: IvÃ¡n Renison, Jam Khan
-/
import LeanVeri.LinearMapPropositions
import LeanVeri.Trace

/-!
This file defines the outer product of two vectors as a linear map,
and proves basic properties of the outer product.
-/

variable (ğ•œ : Type*) {E : Type*} [RCLike ğ•œ]

variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E] [FiniteDimensional ğ•œ E]

/-- The outer product of two vectors -/
def outerProduct (x : E) (y : E) : E â†’â‚—[ğ•œ] E where
  toFun := fun z => (inner ğ•œ y z) â€¢ x
  map_add' z w := by
    rw [â† Module.add_smul, inner_add_right y z w]
  map_smul' m z := by
    rw [RingHom.id_apply, inner_smul_right_eq_smul y z m]
    exact IsScalarTower.smul_assoc m (inner ğ•œ y z) x


omit [FiniteDimensional ğ•œ E] in
lemma outerProduct_def (x : E) (y : E) (z : E) :
    outerProduct ğ•œ x y z = (inner ğ•œ y z) â€¢ x := rfl

omit [FiniteDimensional ğ•œ E] in
/-- The outer product is distributive `(âˆ£xâŸ© + |yâŸ©)âŸ¨z| = âˆ£xâŸ©âŸ¨z| + |yâŸ©âŸ¨z|` -/
lemma outerProduct_add_dist_left (x : E) (y : E) (z : E) :
    outerProduct ğ•œ (x + y) z = outerProduct ğ•œ x z + outerProduct ğ•œ y z := by
  ext
  simp only [LinearMap.add_apply]
  repeat rw [outerProduct_def]
  simp [smul_add]

omit [FiniteDimensional ğ•œ E] in
/-- The outer product is distributive `âˆ£xâŸ©(âŸ¨y| + âŸ¨z|) = âˆ£xâŸ©âŸ¨y| + |xâŸ©âŸ¨z|` -/
lemma outerProduct_add_dist_right (x : E) (y : E) (z : E) :
    outerProduct ğ•œ x (y + z) = outerProduct ğ•œ x y + outerProduct ğ•œ x z := by
  ext
  simp only [LinearMap.add_apply]
  repeat rw [outerProduct_def]
  rw [inner_add_left, add_smul]

omit [FiniteDimensional ğ•œ E] in
/-- The outer product is distributive `(âˆ£xâŸ© - |yâŸ©)âŸ¨z| = âˆ£xâŸ©âŸ¨z| - |yâŸ©âŸ¨z|` -/
lemma outerProduct_sub_dist_left (x : E) (y : E) (z : E) :
    outerProduct ğ•œ (x - y) z = outerProduct ğ•œ x z - outerProduct ğ•œ y z := by
  ext
  simp [LinearMap.add_apply]
  repeat rw [outerProduct_def]
  simp [smul_sub]

omit [FiniteDimensional ğ•œ E] in
/-- The outer product is distributive `âˆ£xâŸ©(âŸ¨y| - âŸ¨z|) = âˆ£xâŸ©âŸ¨y| - |xâŸ©âŸ¨z|` -/
lemma outerProduct_sub_dist_right (x : E) (y : E) (z : E) :
    outerProduct ğ•œ x (y - z) = outerProduct ğ•œ x y - outerProduct ğ•œ x z := by
  ext
  simp [LinearMap.add_apply]
  repeat rw [outerProduct_def]
  rw [inner_sub_left, sub_smul]

omit [FiniteDimensional ğ•œ E] in
/-- The outer product is associative `(âˆ£xâŸ©âŸ¨y|)|zâŸ© = âˆ£xâŸ©âŸ¨y|zâŸ©` -/
lemma outerProduct_assoc_right (x : E) (y : E) (z : E) :
    (outerProduct ğ•œ x y) z = (@inner ğ•œ _ _ y z) â€¢ x := rfl

omit [FiniteDimensional ğ•œ E] in
/-- The outer product scalar multiplication `(c|xâŸ©)âŸ¨y| = c(|xâŸ©âŸ¨y|) `-/
lemma outerProduct_smul_assoc_left (c : ğ•œ) (x : E) (y : E) :
    (outerProduct ğ•œ (c â€¢ x) y) = (c : ğ•œ) â€¢ (outerProduct ğ•œ x y) := by
  ext
  simp only [LinearMap.smul_apply]
  repeat rw [outerProduct_def]
  rw [smul_algebra_smul_comm]

omit [FiniteDimensional ğ•œ E] in
/-- The outer product scalar multiplication `(c|xâŸ©)âŸ¨y| = c(|xâŸ©âŸ¨y|) `-/
lemma outerProduct_smul_assoc_right (c : ğ•œ) (x : E) (y : E) :
    (outerProduct ğ•œ x (c â€¢ y)) = (starRingEnd ğ•œ c) â€¢ (outerProduct ğ•œ x y) := by
  ext
  simp only [LinearMap.smul_apply]
  repeat rw [outerProduct_def]
  rw [starRingEnd_apply, smul_algebra_smul_comm, inner_smul_left, starRingEnd_apply, mul_smul]
  simp only [RCLike.star_def]
  rw [smul_algebra_smul_comm]

lemma adjoint_outerProduct (x y : E) :
    (outerProduct ğ•œ x y).adjoint = outerProduct ğ•œ y x := by
  symm
  rw [LinearMap.eq_adjoint_iff]
  intro v w
  repeat rw [outerProduct_def]
  rw [inner_smul_left, inner_conj_symm, inner_smul_right]
  exact mul_comm _ _

lemma IsSelfAdjoint_outerProduct_self (x : E) :
    IsSelfAdjoint (outerProduct ğ•œ x x) := by
  unfold IsSelfAdjoint
  rw [LinearMap.star_eq_adjoint, adjoint_outerProduct]

lemma IsSymmetric_outerProduct_self (x : E) : (outerProduct ğ•œ x x).IsSymmetric :=
  (outerProduct ğ•œ x x).isSymmetric_iff_isSelfAdjoint.mpr (IsSelfAdjoint_outerProduct_self ğ•œ x)

lemma isPositiveSemiDefinite_outerProduct_self (x : E) :
    (outerProduct ğ•œ x x).isPositiveSemiDefinite := by
  apply And.intro (IsSelfAdjoint_outerProduct_self ğ•œ x)
  intro y
  simp only [outerProduct_def]
  rw [inner_smul_left, InnerProductSpace.conj_inner_symm, inner_mul_symm_re_eq_norm]
  exact norm_nonneg (inner ğ•œ y x * inner ğ•œ x y)

lemma isProjection_outerProduct_self_of_norm_eq_one {x : E} (h : â€–xâ€– = 1) :
    (outerProduct ğ•œ x x).isProjection := by
  apply And.intro (isPositiveSemiDefinite_outerProduct_self ğ•œ x)
  ext y
  simp only [LinearMap.coe_comp, Function.comp_apply, outerProduct_def]
  rw [inner_smul_right, inner_self_eq_norm_sq_to_K, h]
  simp

omit [FiniteDimensional ğ•œ E] in
lemma inner_outerProduct_eq_inner_mul_inner (x y z w : E) :
    inner ğ•œ ((outerProduct ğ•œ x y) z) w = inner ğ•œ z y * inner ğ•œ x w := by
  repeat rw [outerProduct_def]
  rw [inner_smul_left, inner_conj_symm]

omit [FiniteDimensional ğ•œ E] in
lemma outerProduct_comp_outerProduct_eq_inner_smul_outerProduct (x y z w : E) :
    outerProduct ğ•œ x y âˆ˜â‚— outerProduct ğ•œ z w = inner ğ•œ y z â€¢ outerProduct ğ•œ x w := by
  ext v
  simp only [LinearMap.comp_apply, outerProduct_def, map_smul, LinearMap.smul_apply]
  rw [smul_algebra_smul_comm]

omit [FiniteDimensional ğ•œ E] in
lemma outerProduct_mul_outerProduct_eq_inner_smul_outerProduct (x y z w : E) :
    outerProduct ğ•œ x y * outerProduct ğ•œ z w = inner ğ•œ y z â€¢ outerProduct ğ•œ x w := by
  rw [Module.End.mul_eq_comp]
  exact outerProduct_comp_outerProduct_eq_inner_smul_outerProduct ğ•œ x y z w

variable {Î¹ : Type*} [Fintype Î¹] [DecidableEq Î¹]
omit [FiniteDimensional ğ•œ E]

omit [DecidableEq Î¹] in
lemma sum_outerProduct (f g : Î¹ â†’ E) (x : E) :
    (âˆ‘ i, outerProduct ğ•œ (f i) (g i)) x = âˆ‘ i, outerProduct ğ•œ (f i) (g i) x := by
  simp only [LinearMap.coeFn_sum, Finset.sum_apply, LinearMap.sum_apply]

omit [DecidableEq Î¹] in
lemma sum_outerProduct_OrthonormalBasis (b : OrthonormalBasis Î¹ ğ•œ E) :
    âˆ‘i, outerProduct ğ•œ (b i) (b i) = 1 := by
  ext x
  rw [â† LinearIsometryEquiv.map_eq_iff b.repr]
  simp only [LinearMap.coeFn_sum, Finset.sum_apply, Module.End.one_apply, outerProduct_def]
  congr
  exact b.sum_repr' x

lemma trace_outerProduct (x y : E) (b : OrthonormalBasis Î¹ ğ•œ E) :
    LinearMap.trace ğ•œ E (outerProduct ğ•œ x y) = inner ğ•œ y x := by
  rw [(outerProduct ğ•œ x y).trace_eq_sum_inner b]
  simp +contextual [outerProduct_def, inner_smul_right]
  simp +contextual [show âˆ€i, inner ğ•œ y (b i) * inner ğ•œ (b i) x = inner ğ•œ (b i) x * inner ğ•œ y (b i) by intro i; apply mul_comm]
  simp +contextual [â† inner_smul_right, â† outerProduct_def]
  rw [â† inner_sum, â† sum_outerProduct, sum_outerProduct_OrthonormalBasis ğ•œ b, Module.End.one_apply]
